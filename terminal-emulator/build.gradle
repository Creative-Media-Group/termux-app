apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdk = 35
    ndkVersion = '28.0.13004108'
    namespace = 'com.termux'
    defaultConfig {
        minSdk = 29
        targetSdk = 35

        externalNativeBuild {
            ndkBuild {
                cFlags "-std=c11", "-Wall", "-Wextra", "-Werror", "-Os", "-fno-stack-protector", "-Wl,--gc-sections"
            }
        }

        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "src/main/jni/Android.mk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

tasks.withType(Test).configureEach {
    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.9.1'
    testImplementation "junit:junit:4.13.2"
}

tasks.register('sourceJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.findByName("release") ?: components.findByName("default")
                groupId = 'com.termux'
                artifactId = 'terminal-emulator'
                version = '0.118.0'
                artifact(sourceJar)
            }
        }
    }
}
